abstract pattern AggregateMAPE {
	system Sys

	group Main {
		components M, A, P, E
	} 
	
	group Interface {
		managedSyS Sys
		components M, E
	}

	interaction Interface.M -> Main.M [*-SOME,1]
	interaction Main.E -> Interface.E [1,*-SOME]
	interaction Main.M -> Main.A [1,1]
	interaction Main.A -> Main.P [1,1]
	interaction Main.P -> Main.E [1,1]
}

//Aggregate MAPE loop for the concern Heating Comfort
concrete pattern HC_MAPE concretizationOf AggregateMAPE {
	system Heating: AggregateMAPE.Sys
    group MainHC: AggregateMAPE.Main
    group IntHC: AggregateMAPE.Interface
}

//MySmartHome heating system; two managed heating zones: First Floor (FF), Ground floor (GF) 
//Managed items of both systems hs_gf and hs_ff:
//group Number HeatingStatus, group Contact WindowsStatus
//Managed items of system hs_gf:
// group Switch HeatingGF, Number Temperature_Setpoint_GF, Number Temperature_Midpoint_GF, Number Heating_Setting_GF, group Number TemperatureGF, group Contact WindowsFF
//Managed heating system hs_ff:
// group Switch HeatingFF, Number Temperature_Setpoint_FF, Number Temperature_Midpoint_FF, Number Heating_Setting_FF, group Number TemperatureFF, group Contact WindowsFF


configuration MySmartHomeHC instanceOf HC_MAPE {
    hs_ff: HC_MAPE.Heating //managed heating system for First Floor
    hs_gf: HC_MAPE.Heating //managed heating system for Ground Floor

	//main group for the concern Heating Comfort 
	main_hc: HC_MAPE.MainHC {
		components m_hc:M, a_hc:A, p_hc:P, e_hc:E
	}

    //interface group for the ground floor 
	int_hc_gf: HC_MAPE.IntHC {
		managedSyS hs_gf //(heatingGF,state),(windowsGF,state),...
		components m_hc:M, e_hc:E
	}

    //interface group for the first floor
    int_hc_ff: HC_MAPE.IntHC {
		managedSyS hs_ff //(heatingFF,state),(windowsFF,state),...  
		components m_hc:M, e_hc:E
	}

	//inter-interactions
	main_hc.m_hc -> main_hc.a_hc
	main_hc.a_hc -> main_hc.p_hc
	main_hc.p_hc -> main_hc.e_hc
	
	//intra-interactions
	int_hc_gf.m_hc -> main_hc.m_hc
	int_hc_ff.m_hc -> main_hc.m_hc
	main_hc.e_hc -> int_hc_gf.e_hc
	main_hc.e_hc -> int_hc_ff.e_hc
}
